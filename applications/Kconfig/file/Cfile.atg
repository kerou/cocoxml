SECTION license
/*-------------------------------------------------------------------------
Kconfig.atg
Copyright (C) 2008, Charles Wang
Author: Charles Wang  <charlesw123456@gmail.com>
License: LGPLv2
-------------------------------------------------------------------------*/
END.

SCHEME c Cf

SECTION hIncludes
#ifndef  COCO_CFDATA_H
#include "CfData.h"
#endif
END.

COMPILER Cfile

MEMBERS
    CfValueMap_t valmap;

CONSTRUCTOR
    if (!CfValueMap(&self->valmap)) return FALSE;

DESTRUCTOR
    CfValueMap_Destruct(&self->valmap);

CHARACTERS
    letter    = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz_".
    digit     = "0123456789".
    cr        = '\r'.
    lf        = '\n'.
    tab       = '\t'.
    stringCh  = ANY - '"' - '\\' - cr - lf.
    printable = '\x20' .. '\x7e'.
    hexCh     = "0123456789ABCDEFabcdef".

TOKENS
    ident     = "CONFIG_" { letter | digit }.
    number    = digit { digit }.
    hex       = "0x" { hexCh }.
    string    = '"' { stringCh | '\\' printable } '"'.
    eol       = (cr lf) | lf.

IGNORE tab

PRODUCTIONS

Cfile = { Line }.

Line = SetConfig | CommentOrNotSet | eol.

SetConfig       (. char * name, * str; .) =
  ident         (. name = CcsStrdup(self->t->val); .)
  '=' ('y'      (. CfValueMap_SetState(&self->valmap, name, CfYes); .)
      | 'm'     (. CfValueMap_SetState(&self->valmap, name, CfModule); .)
      | string  (. str = CcsUnescape(self->t->val);
      		   CfValueMap_SetString(&self->valmap, name, str);
		   CcsFree(str); .)
      | number  (. CfValueMap_SetInt(&self->valmap, name, self->t->val); .)
      | hex     (. CfValueMap_SetHex(&self->valmap, name, self->t->val); .)
  ) eol         (. CcsFree(name); .)
.

CommentOrNotSet =
  '#' (ident     (. CfValueMap_SetState(&self->valmap, self->t->val, CfNo); .)
  "is" "not" "set" | { ANY }) eol.

END Cfile.
