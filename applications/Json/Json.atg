SECTION license
/*-------------------------------------------------------------------------
Kconfig.atg
Copyright (C) 2008, Charles Wang
Author: Charles Wang  <charlesw123456@gmail.com>
License: LGPLv2
-------------------------------------------------------------------------*/
END.

SCHEME c Json

COMPILER Json

CHARACTERS
    digit    = "0123456789".
    digitH   = "123456789".
    hexCh    = "0123456789ABCDEFabcdef".
    cr       = '\r'.
    lf       = '\n'.
    tab      = '\t'.
    stringCh = ANY - '"' - '\\' - cr - lf.
    escCh    = "\"\\/bfnrt".

TOKENS
    number = [ '-' ] ('0' | digitH { digit }) [ '.' { digit } ] [ ('e' | 'E') [ '+' | '-' ] digit { digit } ].
    string = '"' { stringCh | '\\' escCh | "\\u" hexCh hexCh hexCh hexCh } '"'.

IGNORE cr + lf + tab

PRODUCTIONS

Json = Object.

Object = '{' [ KVPairList ] '}'.

KVPairList = KVPair { ',' KVPair }.

KVPair = string ':' Value.

Array = '[' [ ValueList ] ']'.

ValueList = Value { ',' Value }.

Value = string | number | Object | Array | "true" | "false" | "null".

END Json.
